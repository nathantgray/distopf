clear
Set DefaultBaseFrequency=60

! IEEE 2-bus test case   Y-Y Stepdown Balanced
! Based on script developed by Alan Dunn and Steve Sparling

new circuit.3Bus basekV=4.16 phases=3
! **** HAVE TO STIFFEN THE SOURCE UP A LITTLE; THE TEST CASE ASSUMES AN INFINITE BUS
~ mvasc3=200000 200000

set earthmodel=carson

! **** DEFINE WIRE DATA 
new wiredata.conductor Runits=mi Rac=0.306 GMRunits=ft GMRac=0.0244  Radunits=in Diam=0.721 normamps=530
new wiredata.neutral   Runits=mi Rac=0.592 GMRunits=ft GMRac=0.00814 Radunits=in Diam=0.563 normamps=340

! **** DEFINE LINE GEOMETRY; REDUCE OUT THE NEUTRAL
new linegeometry.4wire nconds=4 nphases=3 reduce=yes 
~ cond=1 wire=conductor units=ft x=-4   h=28 
~ cond=2 wire=conductor units=ft x=-1.5 h=28 
~ cond=3 wire=conductor units=ft x=3    h=28 
~ cond=4 wire=neutral   units=ft x=0    h=24 

! **** 4.16 KV LINE
new line.line1 bus1=sourcebus bus2=n2 geometry=4wire length=2000 units=ft


! **** 4.16 KV LINE
new line.line2 bus1=n2 bus2=n3 geometry=4wire length=2500 units=ft

! **** WYE-CONNECTED 4.16 KV LOAD
new load.load1 phases=3 bus1=n3 conn=wye kV=4.16 kW=5400 pf=0.9  model=1
! **** HAVE TO ALLOW P, Q TO REMAIN CONSTANT TO ABOUT .79 PU -- THIS IS ASSUMED IN TEST CASE
! **** DEFAULT IN DSS IS .95, BELOW WHICH IT REVERTS TO LINEAR MODEL
~ vminpu=0.75    ! model will remain const p,q down to 0.75 pu voltage    

set voltagebases=[12.47, 4.16] 
calcvoltagebases     ! **** let DSS compute voltage bases
set loadmult=1
solve

! Various reports ...
//show voltages LN Nodes 
//show currents resid=yes elements  ! this shows sum of phase currents
//Show Powers kva Elements
